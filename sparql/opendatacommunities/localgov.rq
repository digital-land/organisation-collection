PREFIX localgov: <http://opendatacommunities.org/def/local-government/>
PREFIX admingeo: <http://opendatacommunities.org/ontology/admingeo/>
PREFIX foi: <http://publishmydata.com/def/ontology/foi/>
PREFIX os: <http://data.ordnancesurvey.co.uk/ontology/admingeo/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT * WHERE {

    VALUES ?opendatacommunities_type {

        localgov:FireAuthority
        localgov:PoliceAuthority
        localgov:RegionalParkAuthority
        localgov:TransportAuthority
        localgov:WasteAuthority

        # newer authorities don't have the CivilAdministrativeAuthority type
        localgov:UnitaryAuthority
        localgov:DistrictCouncil
        localgov:LocalAuthority

        # but still need this catch-all for the GLA
        localgov:CivilAdministrativeAuthority

    }
    ?opendatacommunities rdf:type ?opendatacommunities_type .

    { ?opendatacommunities rdfs:label ?name }

    OPTIONAL { ?opendatacommunities os:gssCode ?statistical_geography }

    OPTIONAL { ?opendatacommunities localgov:billingAuthorityCode ?billing_authority }
    OPTIONAL { ?opendatacommunities os:hasCensusCode ?census_area }

    # the local-authority-eng.register.gov.uk is kept and maintained by MHCLG ..

    OPTIONAL { ?opendatacommunities admingeo:gdsCode ?local_authority_eng }
    OPTIONAL { ?opendatacommunities admingeo:gdsTypeCode ?local_authority_type }
    OPTIONAL { ?opendatacommunities admingeo:gdsName ?name }
    OPTIONAL { ?opendatacommunities admingeo:gdsOfficialName ?official_name }
    OPTIONAL { ?opendatacommunities admingeo:startDate ?start_date }
    OPTIONAL { ?opendatacommunities admingeo:endDate ?end_date }
}
ORDER BY ASC(?name)
